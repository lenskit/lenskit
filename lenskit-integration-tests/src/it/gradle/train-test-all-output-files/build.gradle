buildscript {
    repositories {
        maven {
            url project.testRepoURI
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.grouplens.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'

dependencies {
    runtime 'org.hamcrest:hamcrest-library:1.3'
    runtime "org.grouplens.lenskit:lenskit-test:$project.lenskitVersion"
    testRuntime 'com.xlson.groovycsv:groovycsv:1.0'
}

task crossfold(type: Crossfold) {
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    outputFormat "PACK"
    partitionCount 5
    userPartitionMethod holdout(5)
    includeTimestamps false
}

task trainTest(type: TrainTest) {
    maxMemory '256m'
    dataSet crossfold
    cacheDirectory 'cache'
    outputFile 'results.csv'
    userOutputFile 'users.csv'
    algorithm 'Baseline', 'baseline.groovy'
    algorithm 'ItemItem', 'item-item.groovy'
    predict {
        outputFile 'predictions.csv'
        metric 'coverage'
        metric 'rmse'
        metric('ndcg') {
            columnName 'PredRankAcc'
        }
    }
    recommend {
        listSize 10
        outputFile 'recommendations.csv.gz'
        candidateItems 'allItems'
        metric 'length'
        metric 'entropy'
        metric('ndcg') {
            columnName 'nDCG'
        }
        metric('mrr')
        metric 'map'
    }
}

task check(overwrite: true, type: JavaExec) {
    dependsOn trainTest
    classpath configurations.testRuntime

    main 'groovy.ui.GroovyMain'
    args 'verify.groovy'
}
