buildscript {
    repositories {
        maven {
            url project.testRepoURI
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}
import org.lenskit.gradle.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'

dependencies {
    runtime 'org.hamcrest:hamcrest-library:1.3'
    runtime "org.lenskit:lenskit-test:$project.lenskitVersion"
    testRuntime 'com.xlson.groovycsv:groovycsv:1.0'
}

task crossfold(type: Crossfold) {
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    outputFormat "gz"
    partitionCount 5
    userPartitionMethod holdout(5)
    includeTimestamps false
}

task trainTest(type: TrainTest) {
    maxMemory '256m'
    logFile "train-test.log"
    logFileLevel 'DEBUG'
    dataSet crossfold
    cacheDirectory 'cache'
    outputFile 'results.csv'
    userOutputFile 'users.csv'
    algorithm 'Baseline', 'baseline.groovy'
    algorithm 'ItemItem', 'item-item.groovy'
    predict {
        outputFile 'predictions.csv'
        metric 'coverage'
        metric 'rmse'
        metric('ndcg') {
            columnName 'PredRankAcc'
        }
    }
    rank {
        outputFile 'ranks.csv.gz'
        metric 'ndcg'
    }
    recommend {
        listSize 10
        outputFile 'recommendations.csv.gz'
        candidateItems 'allItems'
        excludeItems 'user.trainItems'
        metric 'length'
        metric 'entropy'
        metric('ndcg') {
            columnName 'nDCG-X'
        }
        metric 'popularity'
        metric 'mrr'
        metric 'map'
        metric 'pr'
    }
    // test the top-N metrics for handling a full run with no recommendations
    recommend {
        candidateItems 'allItems'
        excludeItems 'allItems'
        listSize 10
        labelPrefix 'NoRecs'
        metric 'length'
        metric 'entropy'
        metric('ndcg') {
            columnName 'nDCG'
        }
        metric 'popularity'
        metric 'mrr'
        metric 'map'
        metric 'pr'
    }

    doFirst {
        delete 'cache'
    }
}

task runVerifyScript(type: JavaExec) {
    dependsOn trainTest
    classpath configurations.testRuntime

    main 'groovy.ui.GroovyMain'
    args 'verify.groovy'
}

check.dependsOn runVerifyScript

apply from: 'renjin.gradle'