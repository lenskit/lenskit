buildscript {
    repositories {
        maven {
            url "$project.testRepoURI"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.hamcrest:hamcrest-library:1.3'
        classpath "org.grouplens.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*
import static org.hamcrest.MatcherAssert.*
import static org.hamcrest.Matchers.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'

task packRatings(type: PackRatings) {
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    output "$buildDir/ml-100k.pack"
    includeTimestamps false
}
task checkRatingsFileExists(group: 'test') << {
    assertThat "output file exists", file("$buildDir/ml-100k.pack").exists()
}
checkRatingsFileExists.dependsOn packRatings

task testPackRatings(type: LenskitExec) {
    dependsOn packRatings
    mustRunAfter checkRatingsFileExists

    command 'validate-pack'
    args file("$buildDir/ml-100k.pack")
}


check {
    dependsOn checkRatingsFileExists
    dependsOn testPackRatings
}
