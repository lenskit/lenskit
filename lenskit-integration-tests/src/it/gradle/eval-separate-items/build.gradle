buildscript {
    repositories {
        maven {
            url project.testRepoURI
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}
import org.lenskit.gradle.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'

dependencies {
    runtime 'org.hamcrest:hamcrest-library:1.3'
    runtime "org.lenskit:lenskit-test:$project.lenskitVersion"
    testRuntime 'com.xlson.groovycsv:groovycsv:1.0'
}

task crossfold(type: Crossfold) {
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    outputFormat "gz"
    partitionCount 5
    userPartitionMethod holdout(5)
    includeTimestamps false
}

task trainTest(type: TrainTest) {
    maxMemory '256m'
    logFile "train-test.log"
    logFileLevel 'DEBUG'
    dataSet crossfold
    cacheDirectory 'cache'
    outputFile 'results.csv'
    algorithm 'Baseline', 'baseline.groovy'

    recommend {
        listSize 10
        outputFile 'recommendations.csv.gz'
        itemOutputFile 'user-items.csv'
        separateItems true

        candidateItems 'allItems'
        excludeItems 'user.trainItems'
        metric 'length'
        metric 'entropy'
        metric('ndcg') {
            columnName 'nDCG'
        }
        metric 'popularity'
        metric 'mrr'
        metric 'map'
        metric 'pr'
    }
}

apply from: 'renjin.gradle'

verifyR.dependsOn trainTest
verifyR.doFirst {
    if (!file('user-items.csv').exists()) {
        throw new FileNotFoundException("user-item output file not found")
    }
}