// Run some high-level integration tests
file('src/it').eachDir { testDir ->
    if (file("$testDir/ignore").exists()) {
        return
    }

    def name = testDir.name
    def camelName = name.replaceAll(~/(?:^|-)(.)/) {
        it[1].toUpperCase()
    }
    task("run$camelName", type: JavaExec, group: 'verification') {
        description "Run the $name tests."
        dependsOn fetchData
        mustRunAfter test // run the JUnit-based tests first, less overhead
    
        ext.testDir = testDir
        ext.workDir = "$buildDir/it/$name"

        inputs.files testDir, fetchData
        inputs.dir mlDataDir
        outputs.dir workDir
        
        doFirst {
            // clean and re-sync test build
            delete workDir
            copy {
                from testDir
                into workDir
            }
        }

        enableAssertions = true
        workingDir workDir
        classpath configurations.testRuntime
        main 'org.grouplens.lenskit.cli.Main'
        args 'eval', '-j0'
        // tell it where to find the data
        args "-Dlenskit.movielens.100k=$mlDataDir"
        
        if (project.hasProperty('maxTestHeap')) {
            maxHeapSize project.maxTestHeap
        }
    }

    task("verify$camelName", type: JavaExec, group: 'verification') {
        description "Verify the $name tests."
        dependsOn "run$camelName"

        ext.workDir = "$buildDir/it/$name"
        inputs.dir workDir
        
        workingDir workDir
        classpath sourceSets.test.runtimeClasspath
        main 'org.grouplens.lenskit.test.VerifyTestRun'
        args workDir
    }

    if (!skipSlowTests) {
        check.dependsOn "verify$camelName"
    }
}

test.onlyIf { !skipSlowTests }
