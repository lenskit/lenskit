logger.lifecycle 'Enabling lint support in LensKit build'

allprojects {
    apply plugin: 'jacoco'
    jacoco {
        toolVersion = '0.7.0.201403182114'
    }
}

task jacocoMergedReport(type: JacocoReport) {
    for (prj in subprojects) {
        dependsOn prj.test
        sourceSets prj.sourceSets.test
        if (prj.sourceSets.test.allSource.files.isEmpty()) {
            logger.info '{} has no tests', prj.name
        } else {
            logger.info 'aggregating tests from {}', prj.name
            executionData prj.test
        }
    }
    reports {
        csv.enabled = false
        xml.enabled = false
        html.destination = "$reporting.baseDir/jacoco"
    }
}

report.dependsOn jacocoMergedReport

apply plugin: 'sonar-runner'

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "https://sonar.cs.txstate.edu"
        property "sonar.jdbc.driverClassName", "org.postgresql.Driver"
        for (prop in project.properties.entrySet()) {
            if (prop.key.startsWith("sonar.")) {
                property prop.key, prop.value
            }
        }
    }
}
ciPublish.dependsOn 'sonarRunner'
