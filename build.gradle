/*
 * Build system for LensKit, and open-source recommender systems toolkit.
 * Copyright 2010-2014 Regents of the University of Minnesota and contributors
 * Work on LensKit has been funded by the National Science Foundation under
 * grants IIS 05-34939, 08-08692, 08-12148, and 10-17697.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *
 * - Redistributions of source code must retain the above copyright
 *   notice, this list of conditions and the following disclaimer.
 *
 * - Redistributions in binary form must reproduce the above copyright
 *   notice, this list of conditions and the following disclaimer in the
 *   documentation and/or other materials provided with the
 *   distribution.
 *
 * - Neither the name of the University of Minnesota nor the names of
 *   its contributors may be used to endorse or promote products derived
 *   from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
import org.grouplens.lenskit.build.GitExtension
import org.apache.commons.lang3.BooleanUtils

plugins {
    id 'com.gradle.build-scan' version '1.8'
    id "org.sonarqube" version "2.5"
}

apply plugin: 'base'
apply plugin: 'project-report'

ext.webUrl = 'http://lenskit.org'

ext.guavaVersion = '22.0'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

configurations {
    allDeps // collect all external dependencies of all modules
}

task ciPublish {
    description "Runs CI publication tasks (not on Travis)."
}

allprojects { project ->
    group 'org.lenskit'
    version '3.0-SNAPSHOT'

    ext.getConfigProperty = { name, dft ->
        if (project.hasProperty(name)) {
            return project.getProperty(name)
        } else {
            return dft
        }
    }

    repositories {
        if (getConfigProperty('maven.useLocal', 'false').toBoolean()) {
            logger.info 'Using Maven local repository'
            mavenLocal()
        }
        mavenCentral()
        if (project.version.endsWith('-SNAPSHOT')) {
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }
    configurations {
        // the 'support' config will be for build helpers (e.g. JavaDoc taglets)
        support
    }

    tasks.withType(Javadoc).all { task ->
        task.options.encoding = 'UTF-8'
        task.onlyIf {
            !project.getConfigProperty('javadoc.skip', 'false').toBoolean()
        }
    }

    tasks.withType(JavaCompile).each { task ->
        task.options.fork = BooleanUtils.toBoolean(project.findProperty('forkCompilers') ?: 'yes')
    }
    tasks.withType(GroovyCompile).each { task ->
        task.options.fork = BooleanUtils.toBoolean(project.findProperty('forkCompilers') ?: 'yes')
        task.groovyOptions.fork = BooleanUtils.toBoolean(project.findProperty('forkCompilers') ?: 'yes')
    }

    extensions.create('git', GitExtension)
    apply from: "$rootDir/gradle/travis.gradle"

    // Provide support for forcing configuration deps to be resolved
    task resolveAll(group: 'build setup') {
        description 'Resolves and downloads all dependencies'
    }
    ciPrep.dependsOn resolveAll

    configurations.all { cfg ->
        def resolve = task("resolve${cfg.name.capitalize()}") {
            description "Resolve dependencies in the $cfg.name configuration."
            onlyIf {
                cfg.canBeResolved
            }
            doLast {
                logger.info 'resolving {}#{}', project.path, cfg.name
                def result = cfg.resolvedConfiguration
                result.rethrowFailure()
                for (f in result.resolvedArtifacts*.file) {
                    logger.info '{} depends on file {}', cfg.name, f
                }
            }
        }
        resolveAll.dependsOn resolve
    }

    ext.skipSlowTests = project.getConfigProperty('tests.skipSlow', 'false').toBoolean()
}

task reportTests(type: TestReport) {
    destinationDir file("$buildDir/reports/all-tests")
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    // don't package until testing
    tasks.matching({it.name == 'uploadArchives'}).each { task ->
        task.mustRunAfter check
    }
    // defer javadoc until after the build
    tasks.javadoc.mustRunAfter 'classes'

    tasks.withType(JavaCompile).all { task ->
        task.options.encoding = 'UTF-8'
    }
    compileJava {
        options.deprecation = true
        options.compilerArgs << '-Xlint' << '-Xlint:-processing'
    }
    test {
        maxHeapSize = '256m'
        enableAssertions = true
        reports.junitXml.enabled = true
        reports.html.enabled = true

        testLogging {
            showStackTraces true
            exceptionFormat "full"
        }
    }
    reportTests.reportOn test

    // some common deps shared by everything
    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    }

    apply from: "$rootDir/gradle/class-list.gradle"
    apply from: "$rootDir/gradle/license.gradle"

    configurations.compile.dependencies.all { dep ->
        if (!(dep instanceof ProjectDependency)) {
            rootProject.configurations.allDeps.dependencies << dep
        }
    }
    configurations.compileOnly.dependencies.all { dep ->
        if (!(dep instanceof ProjectDependency)) {
            rootProject.configurations.allDeps.dependencies << dep
        }
    }
}

apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/dist.gradle"

task report(group: 'verification', dependsOn: [projectReport]) {
    description 'Run all enabled quality reports.'
}

task tagsFile(type: Exec) {
    ext.dirs = new LinkedHashSet()
    subprojects.each { sp ->
        sp.sourceSets.each { ss ->
            dirs += ss.java.srcDirs
            if (ss.hasProperty('groovy')) {
                dirs += ss.groovy.srcDirs
            }
        }
    }
    dirs.each { inputs.dir it }
    outputs.file 'tags'

    executable 'ctags'
    args '-R'
    args ext.dirs
}

apply from: "$rootDir/gradle/website.gradle"
apply from: "$rootDir/gradle/lint.gradle"

if (file('local.gradle').exists()) {
    apply from: 'local.gradle'
}
